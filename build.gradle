buildscript {
    repositories {
        maven { url = 'https://maven.necauqua.dev' }
    }
    dependencies {
        classpath 'dev.necauqua:necauqua-mod:0.6.0'
    }
}

apply plugin: 'dev.necauqua.nmod'

nmod {
    curseID = '250075'
    version = '1.12-3.0.0.0'
    forge = '1.12.2-14.23.5.2854'
    mappings = '20171003-1.12'
    mixin = '0.8'
    extraMixinConfigs += 'chiseled-me.hack.mixins.json'
}

dependencies {
    // shade in the mixin
    packaged "org.spongepowered:mixin:${nmod.mixin}"
}

// fix dev env runner to immediately run compat mixins, this is part of the gross hack to mix into other mods in 1.12
Collections.replaceAll(minecraft.runs.client.args, 'chiseled-me.hack.mixins.json', 'chiseled-me.compat.mixins.json')


// this is not in my plugin because since 1.13 they FINALLY fixed that
// so this is not universally needed there
afterEvaluate {
    tasks.findByName('prepareRuns')?.doLast {
        copy {
            from 'build/resources/main'
            into 'build/classes/java/main'
        }
    }
}

// another couple of things that are not needed in new Forge (>=1.15) as it comes with Mixin and knows about it
jar.manifest.attributes 'ForceLoadAsMod': 'true', 'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker'

// "fix" that duplicate mod issue (1.12 only I think)
jar.manifest.attributes 'FMLCorePluginContainsFMLMod': 'true'
